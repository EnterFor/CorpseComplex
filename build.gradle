buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
        classpath 'com.wynprice.cursemaven:CurseMaven:2.1.1'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'com.wynprice.cursemaven'

version = "FORGE-${version_minecraft}-${mod_version}"
group = "${mod_group}"
archivesBaseName = "${mod_id}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: "${version_mcp}".toString()

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                corpsecomplex {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                corpsecomplex {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        url 'https://mmdmaven.blamejared.com/'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${version_forge}"
    compileOnly fg.deobf("top.theillusivec4.curios:curios:${version_curios}")
    runtimeOnly fg.deobf("net.darkhax.bookshelf:Bookshelf-${version_minecraft}:${version_bookshelf}")
    compileOnly fg.deobf("net.darkhax.gamestages:GameStages-${version_minecraft}:${version_gamestages}")
    compileOnly fg.deobf("curse.maven:cosmetic-armor-reworked:2943768")
    compile fg.deobf("curse.maven:tool-belt:3086055")
}

sourceSets {
    main {
        resources {
            srcDirs += 'docs'
        }
    }
}

jar {
    manifest {
        attributes(["Specification-Title": "${mod_name}",
                    "Specification-Vendor": "${mod_author}",
                    "Specification-Version": "${version}",
                    "Implementation-Title": "${mod_name}",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor" :"${mod_author}",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

tasks.build.dependsOn sourcesJar

processResources {

    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'version': project.version, 'mod_id' : mod_id, 'mod_name': mod_name, 'mod_url': mod_url, 'mod_author': mod_author, 'mod_description': mod_description, 'mod_icon': mod_icon
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}


curseforge {

    project {
        apiKey = findProperty('curseKey') ?: 0
        id = "${curse_id}"
        releaseType = "${curse_release}"
        changelogType = 'markdown'
        changelog = file('docs/CHANGELOG.md')
        addArtifact(sourcesJar)
    }
}
